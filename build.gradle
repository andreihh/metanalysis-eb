buildscript {
    ext {
        kotlin_version = '1.2.31'
        junit_version = '4.12'
        picocli_version = '3.0.0-alpha-2'
        metanalysis_version = '0.2.6'
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    group = 'org.metanalysis'
    version = '0.2.0'

    apply plugin: 'kotlin'
    apply plugin: 'jacoco'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.jvmTarget = '1.8'
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

        testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        testImplementation "junit:junit:$junit_version"
    }

    jar {
        from rootProject.file('LICENSE')
        from rootProject.file('NOTICE')
    }

    jacocoTestReport {
        dependsOn test

        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }
}

subprojects {
    apply plugin: 'java-library'
}

apply plugin: 'application'

mainClassName = 'org.metanalysis.eb.MainKt'

dependencies {
    implementation project(':metanalysis-eb-core')
    implementation "info.picocli:picocli:$picocli_version"
    runtimeOnly project(':metanalysis-eb-java')
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn {
        allprojects*.jacocoTestReport
    }

    executionData fileTree(project.rootDir.absolutePath)
            .include("**/build/jacoco/*.exec")

    allprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}
